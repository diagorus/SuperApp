plugins {
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.android.kotlin.multiplatform.library)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    kotlin("plugin.serialization") version "2.0.21"
    alias(libs.plugins.ksp)
}

kotlin {
    androidLibrary {
        namespace = "com.lordnikius.superapp.shared"
        compileSdk = 36
        minSdk = 24

        withDeviceTestBuilder {
            sourceSetTreeName = "test"
        }.configure {
            instrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        }
        androidResources.enable = true
    }

    listOf(
        iosX64(),
        iosArm64(),
        iosSimulatorArm64(),
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "sharedKit"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlin.stdlib)

                implementation(compose.runtime)
                implementation(compose.foundation)
                implementation(compose.material3)
                implementation(compose.components.resources)


                implementation(libs.koin.core)
                api(libs.koin.annotations)
                implementation(libs.koin.compose.viewmodel.nav)

                implementation(libs.androidx.datastore.preferences)

                implementation(libs.lifecycle.viewmodel.compose)
                implementation(libs.kermit)

            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlin.test)
            }
        }

        androidMain {
            dependencies {
                implementation(project(":androidLibrary"))

                implementation(libs.androidx.appcompat)
                implementation(libs.material)

                implementation(project.dependencies.platform("androidx.compose:compose-bom:2025.08.00"))

                implementation(libs.androidx.material3)
                implementation(libs.androidx.navigation.compose)

                implementation(libs.kotlinx.serialization.json)

                implementation(libs.coil.compose)
                implementation(libs.coil.network.okhttp)

                implementation(libs.androidx.ui.tooling.preview)
                implementation(libs.androidx.datastore.preferences)

                // Media3
                implementation(libs.androidx.media3.exoplayer)
                implementation(libs.androidx.media3.common.ktx)
                implementation(libs.androidx.media3.datasource)

                implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0")
            }
        }

        getByName("androidDeviceTest") {
            dependencies {
                implementation(libs.androidx.runner)
                implementation(libs.androidx.core)
                implementation(libs.androidx.junit)
            }
        }

        iosMain {
            dependencies {
            }
        }
    }

    sourceSets.named("commonMain").configure {
        kotlin.srcDir("build/generated/ksp/metadata/commonMain/kotlin")
    }
}

dependencies {
    add("kspCommonMainMetadata", libs.koin.ksp.compiler)
    add("kspAndroid", libs.koin.ksp.compiler)
    add("kspIosX64", libs.koin.ksp.compiler)
    add("kspIosArm64", libs.koin.ksp.compiler)
    add("kspIosSimulatorArm64", libs.koin.ksp.compiler)
}

tasks.matching { it.name.startsWith("ksp") && it.name != "kspCommonMainKotlinMetadata" }.configureEach {
    dependsOn("kspCommonMainKotlinMetadata")
}

compose.resources {
    packageOfResClass = "superapp.shared.generated.resources"
}

ksp {
    arg("KOIN_CONFIG_CHECK","true")
}
